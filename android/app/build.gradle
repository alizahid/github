apply plugin: 'com.android.application'

import com.android.build.OutputFile

project.ext.react = [
        entryFile: 'index.js'
]

apply from: '../../node_modules/react-native/react.gradle'

def enableSeparateBuildPerCPUArchitecture = false
def enableProguardInReleaseBuilds = false

def getPassword(String currentUser, String keyChain) {
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()

    exec {
        commandLine 'security', '-q', 'find-generic-password', '-a', currentUser, '-s', keyChain, '-w'
        standardOutput = stdout
        errorOutput = stderr
        ignoreExitValue true
    }

    stdout.toString().trim()
}

def MITTENS_PASSWORD_STORE = getPassword('mittens', 'mittens_password_store')
def MITTENS_PASSWORD_KEY = getPassword('mittens', 'mittens_password_key')

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId 'com.designplox.github'
        minSdkVersion 16
        multiDexEnabled true
        targetSdkVersion 27
        versionCode 1
        versionName '1.0'
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false
            include 'armeabi-v7a', 'x86'
        }
    }
    signingConfigs {
        release {
            if (project.hasProperty('MITTENS_STORE_FILE')) {
                storeFile file(MITTENS_STORE_FILE)
                storePassword MITTENS_PASSWORD_STORE
                keyAlias MITTENS_KEY_ALIAS
                keyPassword MITTENS_PASSWORD_KEY
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionCodes = ['armeabi-v7a': 1, 'x86': 2]
            def abi = output.getFilter(OutputFile.ABI)

            if (abi != null) {
                output.versionCodeOverride = versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
}

dependencies {
    implementation project(':react-native-firebase')

    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:support-v4:27.1.0'
    implementation 'com.android.support:support-media-compat:27.1.0'
    implementation 'com.android.support:appcompat-v7:27.1.0'

    implementation 'com.facebook.react:react-native:+'
}

task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

apply plugin: 'com.google.gms.google-services'
